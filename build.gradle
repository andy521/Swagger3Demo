plugins {
    id 'org.springframework.boot' version '2.3.3.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id 'org.asciidoctor.convert' version '2.3.0'
    id "io.github.lhotari.swagger2markup" version "1.3.3.1"
}

group = 'www.mys.com'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'
tasks.withType(JavaCompile) {
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked"
}

repositories {
    jcenter()
    mavenCentral()
}

ext {
    swaggerVersion = "3.0.0"
    swaggerOutputDir = file("${project.rootDir}/api/swagger")
    asciiDocOutputDir = file("${project.rootDir}/api/asciidoc")
    snippetsOutputDir = file("${project.rootDir}/api/snippets")
}

dependencies {
    // web 依赖
    implementation("org.springframework.boot:spring-boot-starter")
    implementation("org.springframework.boot:spring-boot-starter-web")
    // swagger3 依赖
    implementation("io.springfox:springfox-boot-starter:${swaggerVersion}")
    implementation("io.github.swagger2markup:swagger2markup:1.3.3")
    // 测试 依赖
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    // 文档测试 依赖
    testImplementation("org.springframework.restdocs:spring-restdocs-mockmvc")
}

test {
    systemProperty 'io.springfox.staticdocs.outputDir', swaggerOutputDir
    systemProperty 'io.springfox.staticdocs.snippetsOutputDir', snippetsOutputDir
}

convertSwagger2markup {
    dependsOn test
    swaggerInput "${swaggerOutputDir}/swagger.json"
    outputDir asciiDocOutputDir
    config = [
            'swagger2markup.pathsGroupedBy'                          : 'TAGS',
            'swagger2markup.extensions.springRestDocs.snippetBaseUri': snippetsOutputDir.getAbsolutePath()]
}

asciidoctor {
    dependsOn convertSwagger2markup
    sourceDir = file("${swaggerOutputDir}")
    sources {
        include 'index.adoc'
    }
    backends = ['html5']
    attributes = [
            doctype    : 'book',
            toc        : 'left',
            toclevels  : '3',
            numbered   : '',
            sectlinks  : '',
            sectanchors: '',
            hardbreaks : '',
            generated  : swaggerOutputDir
    ]
    outputDir = swaggerOutputDir
}











